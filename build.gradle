import com.github.whyrising.kmplibtemplate.Ci

plugins {
  alias(deps.plugins.kotlin.multiplatform) apply false
  alias(deps.plugins.spotless)
  alias(deps.plugins.kotlinx.kover)
  alias(deps.plugins.kotest)
}

spotless {
  groovyGradle {
    target "**/*.gradle"
    trimTrailingWhitespace()
    endWithNewline()
    greclipse().configFile('./greclipse.properties')
  }
  kotlin {
    target "**/*.kt"
    ktlint(deps.versions.ktlint.get()).userData([
      "indent_size"    : "2",
      "max_line_length": "80"
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

tasks.koverMergedHtmlReport {
  enabled = true
  htmlReportDir.set(
    layout.buildDirectory.dir("reports/kover/merged-report/html-result"))
}

tasks.koverMergedXmlReport {
  enabled = true
  xmlReportFile.set(
    layout.buildDirectory.file("reports/kover/merged-report/result.xml"))
}

tasks.koverCollectReports {
  outputDir.set(layout.buildDirectory.dir("reports/kover/all-modules-reports"))
}

allprojects {
  group = Ci.group
  version = Ci.publishVersion

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
      events = ["failed", "skipped", "passed"]
      exceptionFormat "full"
    }
  }
}
